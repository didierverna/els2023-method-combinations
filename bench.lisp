(defpackage :els2023-method-combinations/bench
  (:use :cl :els2023-method-combinations)
  (:export :bench))

(in-package :els2023-method-combinations/bench)


(defgeneric test1/raw (i)
  (:method ((i number)) i)
  (:method ((i rational)) i)
  (:method ((i integer)) i)
  (:method ((i fixnum)) i))

(defgeneric! test1/std (i)
  (:method ((i number)) i)
  (:method ((i rational)) i)
  (:method ((i integer)) i)
  (:method ((i fixnum)) i))

(defgeneric test1/raw+ (i)
  (:method-combination +)
  (:method + ((i number)) i)
  (:method + ((i rational)) i)
  (:method + ((i integer)) i)
  (:method + ((i fixnum)) i))

(defgeneric! test1/+ (i)
  (:method-combination :+)
  (:method ((i number)) i)
  (:method ((i rational)) i)
  (:method ((i integer)) i)
  (:method ((i fixnum)) i))


(defgeneric test2/raw (i)
  (:method ((i number)) (format t "Number ~A.~%" i))
  (:method ((i rational)) (format t "Rational ~A.~%" i))
  (:method ((i integer)) (format t "Integer ~A.~%" i))
  (:method ((i fixnum)) (format t "Fixnum ~A.~%" i)))

(defgeneric! test2/std (i)
  (:method ((i number)) (format t "Number ~A.~%" i))
  (:method ((i rational)) (format t "Rational ~A.~%" i))
  (:method ((i integer)) (format t "Integer ~A.~%" i))
  (:method ((i fixnum)) (format t "Fixnum ~A.~%" i)))

(defgeneric test2/raw-progn (i)
  (:method-combination progn)
  (:method progn ((i number)) (format t "Number ~A.~%" i))
  (:method progn ((i rational)) (format t "Rational ~A.~%" i))
  (:method progn ((i integer)) (format t "Integer ~A.~%" i))
  (:method progn ((i fixnum)) (format t "Fixnum ~A.~%" i)))

(defgeneric! test2/progn (i)
  (:method-combination :progn)
  (:method ((i number)) (format t "Number ~A.~%" i))
  (:method ((i rational)) (format t "Rational ~A.~%" i))
  (:method ((i integer)) (format t "Integer ~A.~%" i))
  (:method ((i fixnum)) (format t "Fixnum ~A.~%" i)))


(defun bench ()
  (let ((iterations 100000000)
	;; Save one hash table lookup.
	(+-combination (find-method-combination* :+)))
    (format *trace-output* "Timing the numerical test.~%~%")
    (format *trace-output* "Timing the raw version:~%")
    (time (dotimes (i iterations) (test1/raw 10)))
    (format *trace-output* "Timing the standard version:~%")
    (time (dotimes (i iterations) (test1/std 10)))
    (format *trace-output* "Timing the raw + version:~%")
    (time (dotimes (i iterations) (test1/raw+ 10)))
    (format *trace-output* "Timing the + version:~%")
    (time (dotimes (i iterations) (test1/+ 10)))
    (format *trace-output* "Timing the alternate + version:~%")
    (time (dotimes (i iterations)
	    (call-with-combination +-combination #'test1/std 10))))

  (let ((*standard-output* (make-broadcast-stream))
	(iterations 10000000)
	;; Save one hash table lookup.
	(progn-combination (find-method-combination* :progn)))
    (format *trace-output* "Timing the printing test.~%~%")
    (format *trace-output* "Timing the raw version:~%")
    (time (dotimes (i iterations) (test2/raw 10)))
    (format *trace-output* "Timing the standard version:~%")
    (time (dotimes (i iterations) (test2/std 10)))
    (format *trace-output* "Timing the raw progn version:~%")
    (time (dotimes (i iterations) (test2/raw-progn 10)))
    (format *trace-output* "Timing the progn version:~%")
    (time (dotimes (i iterations) (test2/progn 10)))
    (format *trace-output* "Timing the alternate progn version:~%")
    (time (dotimes (i iterations)
	    (call-with-combination progn-combination #'test2/std 10)))))
